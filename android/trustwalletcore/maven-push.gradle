apply plugin: 'maven-publish'

// TANGEM
def nexusUser = project.property("nexusUser")
def nexusToken = project.property("nexusPass")
// TANGEM
version project.property('version')
group 'com.trustwallet'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/trustwalletcore-release.aar")
            artifact sourcesJar { 
                classifier "sources"
            }
            groupId this.group
            artifactId 'wallet-core'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }

    repositories {
        maven {
            url = uri("https://nexus.tangem-tech.com/repository/maven-releases")
            credentials {
                username = nexusUser
                password = nexusToken
            }
        }
    }
}
